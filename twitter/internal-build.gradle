/*
 * Copyright (C) 2015 Twitter, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply from: rootProject.qualityScript
apply from: rootProject.sizeScript
apply from: rootProject.deployScript
apply plugin: io.fabric.gradle.weaving.FabricPropertiesPlugin
apply plugin: io.fabric.gradle.release.FabricKitReleasePlugin

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        versionCode 1
        versionName version
        minSdkVersion 8
    }

    lintOptions {
        warningsAsErrors true
    }

    buildTypes {
        debug {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""

            testCoverageEnabled enableTestCodeCoverage
        }

        release {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""
        }
    }

    jacoco {
        version = jacocoVersion
    }

    //Gradle publishes only release by default for multi module projects
    //This will publish all build variants and we can specifically choose them
    defaultPublishConfig 'release'
    publishNonDefault true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

ext.projectDependencies = [
        ':sdk:twitter:twitter-core',
        ':sdk:twitter:tweet-ui',
        ':sdk:twitter:tweet-composer',
        ':sdk:digits:digits',
        ':sdk:fabric:fabric'
]

dependencies {
    projectDependencies.each {
        debugCompile project(path: it, configuration: 'debug')
        releaseCompile kitDependency(path: it, configuration: 'release')
    }

    testCompile rootProject.mockito
    testCompile rootProject.junit
    testCompile rootProject.mavenAntTasks
    testCompile(rootProject.robolectric) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

fabricKitRelease {
    apiKey = project.hasProperty('TWITTER_KIT_API_KEY') ? project.TWITTER_KIT_API_KEY : ""
    friendlyName = "Twitter"
    name = "${project.pom_group_id}.${project.pom_artifact_id}"
    description = "Integrate Log in with Twitter, embed Tweets, and leverage the Twitter API."
    version = project.version
    artifactUrl = "https://maven.fabric.io/repo/com/twitter/sdk/android/twitter/${project.version}/twitter-${project.version}.pom"
    releaseNotesUrl = "https://s3.amazonaws.com/kits-crashlytics-com/android/com/twitter/sdk/android/twitter/${project.version}/twitter-${project.version}-release-notes.html"
    createdAt = System.currentTimeMillis()
    learnMoreUrl = "https://dev.twitter.com/twitter"
    price {
        plan = "free"
    }
    license {
        name = "Twitter"
        url = "https://twitter.com/tos"
    }
    releaseChannel = "public"
    minBuild = 0
    maxBuild = Integer.MAX_VALUE
    minPlatformVersion {
        apiVersion "8"
    }
}

project.tasks.getByName('fabricKitRelease').enabled = rootProject.ext.projectsForRelease.contains(project.path)